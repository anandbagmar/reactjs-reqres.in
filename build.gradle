import java.text.SimpleDateFormat

buildscript {
    ext {
        gradleVersionProperty = '8.2.1'
        masterThoughtVersion = '5.7.6'
        testNGVersion = '7.8.0'
        webdriverManagerVersion = '5.4.0'
        jsonSimpleVersion = '1.1.1'
        applitoolsVersion = '5.63.0'
        restAssuredVersion = '5.3.1'
    }
}

plugins {
    id "java"
    id "idea"
    id "eclipse"
}

group 'com.anandbagmar'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

sourceSets {
    test {
        java {
            srcDir file('src/e2eTest/java')
        }
        resources {
            srcDir file('src/e2eTest/resources')
        }
    }
}

dependencies {
    implementation 'org.testng:testng:' + testNGVersion
    implementation 'net.masterthought:cucumber-reporting:' + masterThoughtVersion
    implementation 'io.github.bonigarcia:webdrivermanager:' + webdriverManagerVersion
    implementation 'com.googlecode.json-simple:json-simple:' + jsonSimpleVersion
    implementation 'com.applitools:eyes-selenium-java5:' + applitoolsVersion
    implementation 'io.rest-assured:rest-assured:' + restAssuredVersion
}

static def getCurrentDatestamp() {
    Date today = new Date()
    SimpleDateFormat df = new SimpleDateFormat("MM-dd-yyyy")
    return df.format(today)
}

static def getCurrentTimestamp() {
    Date today = new Date()
    SimpleDateFormat df = new SimpleDateFormat("HH-mm-ss")
    return df.format(today)
}

project.ext.logDir = "${rootDir}/reports/" + getCurrentDatestamp() + "/" + getCurrentTimestamp()

test {
    doFirst {
        environment "APPLITOOLS_DONT_CLOSE_BATCHES", "true"
        environment "APPLITOOLS_TUNNEL", "true"
    }
    useTestNG() {
        options {
            parallel = 'methods'
            threadCount = 2
        }
        reports.html.required = true
        reports.html.outputLocation = file("${project.logDir}/html")

        reports.junitXml.required = true
        reports.junitXml.outputLocation = file("${project.logDir}/junit")
        testLogging {
            events "started", "passed", "skipped", "failed"
            // show standard out and standard error of the test JVM(s) on the console
            showStandardStreams = true
            // show full exception trace
            exceptionFormat = 'full'
            showStackTraces = true
            showCauses = true
            showExceptions = true
        }
    }
}

clean.doFirst {
    delete "${rootDir}/target"
    delete "${rootDir}/bin"
    delete "${rootDir}/build"
    delete "${rootDir}/out"
    println "Clean: Deleted bin, build, out, target and reports directories"
}

clean.doLast {
    mkdir "${rootDir}/reports"
    println "Created reports directory"
}

wrapper {
    gradleVersion = gradleVersionProperty
}